{
  description = "Flake for development workflows.";

  inputs = {
    flake-utils.url = "github:numtide/flake-utils";
    rainix.url = "github:rainlanguage/rainix";
    rain.url = "github:rainlanguage/rain.cli";
    # rain.cli pulls its own rainix; make it follow ours so the lock has a
    # single rainix (and one rust toolchain / nixpkgs) instead of two revs.
    rain.inputs.rainix.follows = "rainix";
  };

  outputs =
    {
      flake-utils,
      rainix,
      rain,
      ...
    }:
    flake-utils.lib.eachDefaultSystem (
      system:
      let
        pkgs = rainix.pkgs.${system};
      in
      rec {
        packages = rec {
          rainlang-prelude = rainix.mkTask.${system} {
            name = "rainlang-prelude";
            body = ''
              set -euxo pipefail

              # Needed by deploy script.
              mkdir -p deployments/latest;

              # Build metadata that is needed for deployments.
              mkdir -p meta;
              forge script --silent ./script/BuildAuthoringMeta.sol;
              rain meta build \
                -i <(cat ./meta/AuthoringMeta.rain.meta) \
                -m authoring-meta-v2 \
                -t cbor \
                -e deflate \
                -l none \
                -o meta/RainlangExpressionDeployer.rain.meta \
            '';
            additionalBuildInputs = rainix.sol-build-inputs.${system} ++ [ rain.defaultPackage.${system} ];
          };

          test-wasm-build = rainix.mkTask.${system} {
            name = "test-wasm-build";
            body = ''
              set -euxo pipefail

              cargo build --target wasm32-unknown-unknown --exclude rainlang-cli --workspace
            '';
          };
        }
        // rainix.packages.${system};

        devShells.default = pkgs.mkShell {
          inherit (rainix.devShells.${system}.default) shellHook;
          packages = [
            packages.rainlang-prelude
            packages.test-wasm-build
          ];
          inputsFrom = [ rainix.devShells.${system}.default ];
        };
      }
    );
}
